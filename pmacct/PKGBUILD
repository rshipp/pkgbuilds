# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: xav <xav at ethertricks dot net>

pkgname=pmacct
pkgver=0.14.0
pkgrel=1
pkgdesc='Accounting and aggregation toolsuite for IPv4 and IPv6 able to collect data through libpcap, Netlink/ULOG, Netflow and sFlow.'
arch=('i686' 'x86_64')
url='http://www.pmacct.net/'
license=('GPL2')
depends=('libpcap' 'libmysqlclient' 'postgresql-libs' 'sqlite3')
source=("http://www.pmacct.net/${pkgname}-${pkgver}.tar.gz"
        'pmacctd.rc.d'
        'nfacctd.rc.d'
        'sfacctd.rc.d'
        'uacctd.rc.d')
sha1sums=('0b1cd2f04294af4ce1586f8ddd55523f9f2fb72f'
          '084404167416e8e5e5d4736a68e37a0bfb9bfa58'
          '98483e508a8100425c5ad941a2025bbd034c4ba9'
          'c2171c1f64b956a56eaa5955d723b0778ba01c79'
          '089a63e02d07ea61c8e7c265b955ac587969c814')

build() {  
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-ipv6 \
              --enable-mysql \
              --enable-pgsql \
              --enable-sqlite3 \
              --enable-64bit \
              --enable-threads
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -dm755 "${pkgdir}/etc/pmacct/examples" 
  install -dm755 "${pkgdir}/usr/share/pmacct/"{mysql,pgsql,sqlite3,sh} 
  install -dm755 "${pkgdir}/usr/share/doc/pmacct"
  
  # configuration examples
  install -Dm644 examples/pmacctd-imt.conf.example "${pkgdir}/etc/pmacct/pmacctd.conf"
  install -Dm644 examples/*.example "${pkgdir}/etc/pmacct/examples/"
  
  # rc.d scripts
  install -Dm755 "${startdir}/sfacctd.rc.d" "${pkgdir}/etc/rc.d/sfacctd"
  install -Dm755 "${startdir}/nfacctd.rc.d" "${pkgdir}/etc/rc.d/nfacctd"
  install -Dm755 "${startdir}/nfacctd.rc.d" "${pkgdir}/etc/rc.d/uacctd"
  install -Dm755 "${startdir}/pmacctd.rc.d" "${pkgdir}/etc/rc.d/pmacctd"
  
  # sh and sql scripts
  install -Dm644 sql/*.mysql "${pkgdir}/usr/share/pmacct/mysql/"
  install -Dm644 sql/*.pgsql "${pkgdir}/usr/share/pmacct/pgsql/"
  install -Dm644 sql/*.sqlite3 "${pkgdir}/usr/share/pmacct/sqlite3/"
  install -Dm744 examples/*.sh "${pkgdir}/usr/share/pmacct/sh/"

  # documentation
  install -Dm644 sql/README.* "${pkgdir}/usr/share/doc/pmacct/"
  install -Dm644 docs/* "${pkgdir}/usr/share/doc/pmacct/"
  install -Dm644 AUTHORS ChangeLog CONFIG-KEYS COPYING EXAMPLES FAQS KNOWN-BUGS \
                 README TODO TOOLS UPGRADE "${pkgdir}/usr/share/doc/pmacct/"
}
