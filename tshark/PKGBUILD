# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>

pkgname=tshark
_pkgname=wireshark
pkgver=1.8.1
pkgrel=1
pkgdesc='A free network protocol analyzer for Unix/Linux - command-line frontend only.'
arch=('i686' 'x86_64')
url='http://www.wireshark.org/'
license=('GPL2')
makedepends=('bison' 'flex')
depends=('krb5' 'libcap' 'libpcap' 'bash' 'gnutls' 'libgcrypt' 'glib2' 'lua')
conflicts=('wireshark' 'wireshark-gtk' 'wireshark-cli')
provides=('wireshark-cli')
replaces=('wireshark-cli')
install=wireshark.install
options=(!libtool)
source=("http://www.wireshark.org/download/src/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('629fe03f4a64eb8c7bd9308ee4e22e2e')

build() {
  cd "${_pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-ssl \
              --with-zlib=yes \
              --with-lua \
              --enable-wireshark=no
  make all
}

package() {
  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Headers
  install -dm755 "${pkgdir}/usr/include/wireshark/"{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}
  install -m644 color.h config.h register.h "${pkgdir}/usr/include/wireshark"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}/usr/include/wireshark/${d}"
  done

  # wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
}
