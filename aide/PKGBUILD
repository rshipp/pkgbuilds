# Maintainer: george <rpubaddr0@gmail.com>
# Contributer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.copm>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us> 
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=aide
pkgver=0.15.1
pkgrel=1
pkgdesc='A file integrity checker and intrusion detection program.'
arch=('i686' 'x86_64')
url='http://www.cs.tut.fi/~rammer/aide.html'
license=('GPL')
makedepends=('mhash' 'elfutils')
backup=('etc/aide.conf')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'aide.conf')
md5sums=('d0b72535ff68b93a648e4d08b0ed7f07'
         'd3ac69ad7c12c1686f8accf2717139f6')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --with-mhash \
              --with-posix-acl --with-prelink --with-xattr \
              --with-zlib --with-e2fsattrs
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/etc/aide.conf"
  install -Dm644 "${srcdir}/aide.conf" "${pkgdir}/etc/aide.conf"
  install -dm755 "${pkgdir}/var/"{log,lib}/aide/
}
