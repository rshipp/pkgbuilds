# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: zoulnix <http://goo.gl/HQaP>

pkgname=labrea
pkgver=2.5_stable_1
_pkgver=2.5-stable-1
pkgrel=1
pkgdesc='Intrusion detection tool to detect and trap worms, crackers and other malware.'
arch=('i686' 'x86_64')
url='http://labrea.sourceforge.net/'
license=('GPL')
depends=('libdnet' 'libpcap')
makedepends=('autoconf' 'automake' 'gcc' 'make' 'patch' 'pkg-config')
options=('!libtool')
backup=('etc/labrea.conf')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.gz"
        "${pkgname}.conf.d"
        "${pkgname}.logrotate.d"
        "${pkgname}.rc.d"
        "${pkgname}-incfix.diff")
md5sums=('e76d506e82b60cc5477ccee1b3368cda'
         'a8138e1e0d4618f9bc5dcc0f29bf52ad'
         '03813267a5dd15486e2b743abce253c1'
         'ac19bef7695106b75833ca643b7b4a26'
         '9db8e6ea1d5de046b86c038c73d14db9')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  # fixes compile error on 32bit
  patch -Np1 -i "${srcdir}/${pkgname}-incfix.diff"

  # Generating build system
  sh autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  install -dm755 "${pkgdir}/var/log" "${pkgdir}/etc/"{conf.d,logrotate.d,rc.d}

  make DESTDIR="${pkgdir}" install

  # touch logs
  touch "${pkgdir}/var/log/labrea.log"

  install -m644 "${startdir}/${pkgname}.conf.d" "${pkgdir}/etc/conf.d/${pkgname}"
  install -m644 "${startdir}/${pkgname}.logrotate.d" "${pkgdir}/etc/logrotate.d/${pkgname}"
  install -m755 "${startdir}/${pkgname}.rc.d" "${pkgdir}/etc/rc.d/${pkgname}"
}
