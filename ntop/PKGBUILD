# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=ntop
pkgver=5.0.1
pkgrel=1
pkgdesc='A network traffic probe that shows the network usage.'
arch=('i686' 'x86_64')
url='http://www.ntop.org/'
license=('GPL')
depends=('libevent' 'libpcap' 'gd' 'glibc' 'libxml2' 'openssl' 
         'rrdtool' 'pcre' 'geoip' 'lua')
makedepends=('subversion' 'ca-certificates')
options=('!libtool' '!makeflags')
install=$pkgname.install
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/Stable/${pkgname}-${pkgver}.tar.gz"
        'ntop'
        'ntop.conf.d'
        'ntop.service')
md5sums=('01710b6925a8a5ffe1a41b8b512ebd69'
         '7b0d7bb57432e768ff387e9f7236f87c'
         '6a9371de1dcf31d8ef13a4cf349c02b3'
         'bcb2bcfb4b573babdee7d9321591e3e3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  rm -rf nDPI
  echo 't' | svn export -r 5614 'https://svn.ntop.org/svn/ntop/trunk/nDPI/'
  tar -czvf nDPI-svn5614.tar.gz nDPI

  ./autogen.sh --noconfig
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var/lib \
              --disable-snmp

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install-recursive

  install -dm0755 -o nobody -g nobody "${pkgdir}/var/lib/ntop"
  install -dm0755 -o nobody -g nobody "${pkgdir}/var/lib/ntop/rrd"
  install -Dm0755 "${srcdir}/ntop" "${pkgdir}/etc/rc.d/ntop"
  install -Dm0644 "${srcdir}/ntop.conf.d" "${pkgdir}/etc/conf.d/ntop"

  for _f in "${pkgdir}/usr/lib/ntop/plugins/"*.so; do
    _plug="$(basename ${_f})"
    ln -sf "../../lib${_plug}" "${_f}"
  done

  install -Dm0644 "${srcdir}/ntop.service" \
    "${pkgdir}/usr/lib/systemd/system/ntop.service"
}
