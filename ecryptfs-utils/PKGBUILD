# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ecryptfs-utils
pkgver=96
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Enterprise-class stacked cryptographic filesystem for Linux.'
url='https://launchpad.net/ecryptfs'
license=('GPL')
makedepends=('swig' 'intltool' 'gettext')
depends=('keyutils' 'nss' 'python2')
source=("http://launchpad.net/ecryptfs/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz")
options=(!libtool)
md5sums=('4f92c9f6e8f62ac00ca7e2c4e480f1d7')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr PYTHON=python2
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod +s "${pkgdir}/sbin/mount.ecryptfs_private"
}
