# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ecryptfs-utils
pkgver=100
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Enterprise-class stacked cryptographic filesystem for Linux.'
url='https://launchpad.net/ecryptfs'
license=('GPL')
makedepends=('swig' 'intltool' 'gettext')
depends=('keyutils' 'nss' 'python2')
source=("http://launchpad.net/ecryptfs/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz")
options=(!libtool)
md5sums=('efaa5eabbd368268874536036397f73b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod +s "${pkgdir}/sbin/mount.ecryptfs_private"
  install -Dm755 "${pkgdir}/lib/security/pam_ecryptfs.so" \
    "${pkgdir}/usr/lib/security/pam_ecryptfs.so"
  rm "${pkgdir}/lib/security/pam_ecryptfs.so" &&
    ln -sv /usr/lib/security/pam_ecryptfs.so \
      "${pkgdir}/lib/security/pam_ecryptfs.so"
}
