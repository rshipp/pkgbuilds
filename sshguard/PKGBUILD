# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=sshguard
pkgver=1.5
pkgrel=1
pkgdesc='Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with iptables.'
arch=('i686' 'x86_64')
url="http://www.sshguard.net/"
license=('GPL')
depends=('iptables')
options=("zipman")
install=sshguard.install
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'sshguard.rc'
        'sshguard.conf.d')
md5sums=('11b9f47f9051e25bdfe84a365c961ec1'
         '07bffbbfbf452d09cda10a1bab8cc03b'
         '1dc6604975d3cd51321a8ad7cd53947c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-firewall=iptables
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  strings src/sshguard | grep -E "^`which iptables`[[:space:]]" >/dev/null
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm0755 "${srcdir}/sshguard.rc" "${pkgdir}/etc/rc.d/sshguard"
  install -Dm0644 "${srcdir}/sshguard.conf.d" "${pkgdir}/etc/conf.d/sshguard"
}
