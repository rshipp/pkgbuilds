# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sslsniff
pkgver=0.8
pkgrel=1
pkgdesc='A tool to MITM all SSL connections on a LAN and dynamically generate certs for the domains that are being accessed.'
arch=('i686' 'x86_64')
url='http://www.thoughtcrime.org/software/sslsniff/'
license=('custom')
depends=('boost' 'log4cpp' 'dsniff')
source=("http://www.thoughtcrime.org/software/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'boost-1.47.patch')
md5sums=('030fe31af33c22a932393c7a5f33bb2e'
         '439f5cd6a853e6b279a4cb1a89cec54d')

build() {
  cd "${pkgname}-${pkgver}"
  patch -p0 < "${srcdir}/boost-1.47.patch"
  LDFLAGS+='-lcrypto -lboost_system' ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Package some example material.
  install -dm755 "${pkgdir}/usr/share/${pkgname}/"{updates,certs}
  install -Dm644 leafcert.pem IPSCACLASEA1.crt "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 updates/*xml "${pkgdir}/usr/share/${pkgname}/updates"
  install -Dm644 certs/* "${pkgdir}/usr/share/${pkgname}/certs"
  
  # Install license.
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
