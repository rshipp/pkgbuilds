# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: CRT <crt.011@gmail.com>
# Contributor:  SÃ©bastien Duquette <ekse.0x@gmail.com>
# Contributor: icarus <icarus.roaming@gmail.com>
# Contributor: Stephane Travostino <stephane.travostino@gmail.com>

pkgname=cowpatty
pkgver=4.6
pkgrel=1
pkgdesc='Wireless WPA/WPA2 PSK handshake cracking utility.'
arch=('i686' 'x86_64')
url='http://www.willhackforsushi.com/?page_id=50'
license=('GPL')
depends=('openssl' 'libpcap')
options=(docs)
source=("http://www.willhackforsushi.com/code/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tgz")
md5sums=('b90fd36ad987c99e7cc1d2a05a565cbd')

build() {
  cd "${pkgname}-${pkgver}"
  make 
  make strip 
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" BINDIR="/usr/bin/" install
  install -Dm644 dict "${pkgdir}/usr/share/cowpatty/dict"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
