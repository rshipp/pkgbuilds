# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-io-tty
_realname=IO-Tty
pkgver=1.12
pkgrel=1
pkgdesc='Provide an interface to TTYs and PTYs.'
arch=('i686' 'x86_64')
url='http://search.cpan.org/dist/IO-Tty/'
license=('GPL' 'PerlArtistic')
depends=('glibc')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/${_realname}-${pkgver}.tar.gz")
md5sums=('11695a1a516b3bd1b90ce75ff0ce3e6d')
options=('!emptydirs')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor 
  make
}

package() { 
  cd "${srcdir}/${_realname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
