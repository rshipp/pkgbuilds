# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>

pkgname=hydra
pkgver=7.3
pkgrel=1
pkgdesc='A very fast network logon cracker which supports many different services.'
arch=('i686' 'x86_64')
url='http://www.thc.org/thc-hydra/'
license=('GPL3')
depends=('krb5' 'openssl' 'postgresql-libs' 'subversion' 'apr' 
         'apr-util' 'libssh' 'pcre' 'libmysqlclient' 'libidn')
optdepends=('flamerobin: pulls gtk depends, for the optional gui')
source=("http://freeworld.thc.org/releases/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('ac17b395d6af5ee56726b4d3b24580fb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
} 

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/"{bin,share/man/man1,man/man1}
  make PREFIX="${pkgdir}/usr" install
  mv "${pkgdir}/usr/man/man1/"* "${pkgdir}/usr/share/man/man1/"
  rm -rf "${pkgdir}/usr/man"
}
