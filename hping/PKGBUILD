# Maintainer: george <rpubaddr0@gmail.com>
# Contributer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=hping
_pkgname=hping3
pkgver=3.0.0
_pkgver=20051105
pkgrel=1
pkgdesc='A command-line oriented TCP/IP packet assembler/analyzer.'
arch=('i686' 'x86_64')
url='http://www.hping.org'
license=('GPL2' 'BSD')
depends=('libpcap')
provides=('hping3')
source=("http://www.hping.org/${_pkgname}-${_pkgver}.tar.gz"
        'Makefile.patch'
        'bytesex.h.patch')
md5sums=('ca4ea4e34bcc2162aedf25df8b2d1747'
         '3c6f920201fc980d377408917a28df93'
         '8af8e336819df1447b0c1b879a704be9')

build() {
  cd "${_pkgname}-${_pkgver}"
  [[ "$CARCH" == "x86_64" ]] && patch -Np1 -i ../bytesex.h.patch
  MANPATH=/usr/share/man ./configure --no-tcl
  make
}

package() {
  cd "${_pkgname}-${_pkgver}"
  patch -p1 < ../Makefile.patch
  make DESTDIR="${pkgdir}" install
  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
