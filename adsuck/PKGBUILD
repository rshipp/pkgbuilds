# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Dan Serban
# Contributor: Mimas <mimasgpc+pkgbuild@free.fr> (http://forums.archlinux.fr/topic5585.html)
# Contributor: acoolon <@web.de>

pkgname=adsuck
pkgver=2.4.2
pkgrel=1
pkgdesc='A DNS filter that spoofs blacklisted addresses and forwards all other queries.'
arch=('i686' 'x86_64')
url='http://opensource.conformal.com/wiki/Adsuck'
license=("BSD")
depends=('ldns' 'libevent')
install=$pkgname.install
source=("http://opensource.conformal.com/snapshots/${pkgname}/${pkgname}-${pkgver}.tgz"
        '50-adsuck-update' 'adsuck.rc' 'filter')
md5sums=('ecdf08c6c2b232152ef3b5b5e761d8ad'
         'dda609d6bdf1007cc7a1aaf465087071'
         '08053930f55cc4bd7ca758d70d180c18'
         '9b62235b449594a27388aaabcaf5ba8d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -f GNUmakefile adsuck
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm700 adsuck "${pkgdir}/usr/sbin/adsuck"
  install -Dm644 adsuck.8 "${pkgdir}/usr/share/man/man8/adsuck.8"
  install -Dm644 "${srcdir}/filter" "${pkgdir}/var/adsuck/filter"
  install -Dm755 "${srcdir}/adsuck.rc" "${pkgdir}/etc/rc.d/adsuck"
  # uncomment if you want it
  #install -D -m755 "${srcdir}/50-adsuck-update" "${pkgdir}/usr/lib/dhcpcd/dhcpcd-hooks/50-adsuck-update"
}
