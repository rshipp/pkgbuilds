# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: MrMars <mr[dot]mars[dot]mail [at] gmail [dot] com>
# Contributor: Roman Zimbelmann <romanz@lavabit.com>
 
pkgname=ranger
pkgver=1.5.3
pkgrel=1
pkgdesc='A simple, vim-like file manager'
arch=('any')
url='http://ranger.nongnu.org'
license=('GPL')
depends=('python')
optdepends=('python-chardet: helps in case of encoding detection problems'
            'file: for determining file types'
            'sudo: to use the run as root feature'
            'libcaca: for previewing images'
            'lynx: preview html pages'
            'elinks: preview html pages'
            'w3m: preview html pages'
            'transmission-cli: for viewing bittorrent info')
conflicts=('ranger-git')
screenshot='http://ranger.nongnu.org/screenshots/screenshot0.png'
source=("http://ranger.nongnu.org/${pkgname}-${pkgver}.tar.gz")
md5sums=('ab90a2aa4d77cacbd47f45ed6b7c58f2')
 
package() { 
  cd "${pkgname}-${pkgver}"
  # Replace "python" with the python binary you want ranger to use!
  # (python 2.6 and >=3.1 supported, below 2.6 will certainly not work.)
  python setup.py -q install --root="${pkgdir}" --optimize=1
}
