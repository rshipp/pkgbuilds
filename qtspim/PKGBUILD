# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: d4n1x <danlo8600@gmail.com>

pkgname=qtspim
pkgver=0.9.13
_pkgver=0_9_1
pkgrel=1
pkgdesc='New user interface for spim, a MIPS simulator.'
arch=('i686' 'x86_64')
url='http://pages.cs.wisc.edu/~larus/spim.html'
license=('BSD')
depends=('qt')
source=("http://pages.cs.wisc.edu/~larus/SPIM/${pkgname}-${_pkgver}.tar.gz"
        'update_view.patch' '64bit_support.patch')
md5sums=('7f3d88974e9f2d2c96149b121d047e84'
         'bf58bf42cf1a58ed1d99ba80cde9b63a'
         '468c0d7e4137b4cc177044d621def111')

build() {
  cd "${srcdir}/${pkgname}-0_9"

  # fix build, taken from:
  # https://build.opensuse.org/package/files?package=QtSpim&project=home%3AetamPL
  patch -Np1 < "${srcdir}/64bit_support.patch"
  # show the current position in code and update registers when doing simgle step
  # done by myself :-P
  patch -Np1 < "${srcdir}/update_view.patch"

  cd QtSpim
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-0_9"
  install -D -m644 README \
    "${pkgdir}/usr/share/licenses/${pkgname}/README"
  
  cd QtSpim
  install -d -m755 "${pkgdir}/usr/bin/"
  install -m755 QtSpim "${pkgdir}/usr/bin"
}

# vim:set ts=2 sw=2 et:
