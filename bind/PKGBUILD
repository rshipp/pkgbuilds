# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Mario Vazquez <mario_vazq@hotmail.com>

pkgname=bind
pkgver=9.9.1_P1
_pkgver=9.9.1-P1
pkgrel=1

pkgdesc='Berkeley Internet Name Daemon, the reference implementation of the DNS protocols.'
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
arch=('i686' 'x86_64')
options=('!makeflags' '!libtool')
depends=('openssl' 'krb5' 'libxml2')
provides=('dns-server')
install=$pkgname.install
backup=('etc/logrotate.d/named'
        'etc/conf.d/named'
        'etc/named.conf')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"
        'root.hint::http://www.internic.net/zones/named.root'
        'remove-tools.patch'
        'named'
        'named.conf'
        'named.conf.d'
        'named.logrotate'
        'localhost.zone'
        '127.0.0.zone')
sha1sums=('a2263b96ccd8a143ea54b39958142c542bf605a8'
          'cbdecd1ea8e7b612b33b63d66bc57273795d1f54'
          '18f477f510df31e6c6f547ee2d5fc46e973479bf'
          '46232e9db243c6c05e170a1781d7a7f413be5d03'
          '5ca7a5f2a132548a090a045a2df3acea6b35d9eb'
          '7848edbfb9a848843f57c11c02b0289eefd42d00'
          '9ffb5c3f72390a517aeae557e32349d5d278cb63'
          '76a0d4cd1b913db177a5a375bebc47e5956866ec'
          '53be0f1437ebe595240d8dbdd819939582b97fb9')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  patch -p1 -i ../remove-tools.patch

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --disable-linux-caps \
              --with-openssl \
              --with-libxml2 \
              --with-libtool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  make DESTDIR="${pkgdir}" install

  rmdir "${pkgdir}/var/run"
  install -dm755 "${pkgdir}/usr/share/doc/bind"
  install doc/arm/*.html "${pkgdir}/usr/share/doc/bind/"

  install -Dm755 ../named "${pkgdir}/etc/rc.d/named"
  install -Dm644 ../named.conf.d "${pkgdir}/etc/conf.d/named"
  install -Dm600 ../named.logrotate "${pkgdir}/etc/logrotate.d/named"
  install -Dm640 -o 0 -g 40 ../named.conf "${pkgdir}/etc/named.conf"

  install -dm750 -o 0 -g 40 "${pkgdir}/var/named"
  install -Dm640 -o 0 -g 40 ../root.hint "${pkgdir}/var/named/"
  install -Dm640 -o 0 -g 40 ../127.0.0.zone "${pkgdir}/var/named/"
  install -Dm640 -o 0 -g 40 ../localhost.zone "${pkgdir}/var/named/"

  # prevent conflicts with dnsutils
  rm "${pkgdir}/usr/bin/nsupdate"
  rm "${pkgdir}/usr/share/man/man1/nsupdate.1"
}
