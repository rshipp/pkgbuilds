# Maintainer: george <rpubaddro {at} gmail [dot] com>
# Contributor: Evan Anderson <evan@thelinuxman.us>

pkgname=linconnect-server-git
pkgver=r100.4d746b2
pkgrel=1
pkgdesc='Server for the linconnect Android app that mirrors notifications to the desktop.'
url='https://github.com/hauckwill/linconnect-server'
license=('GPL3')
arch=('any')
install=${pkgname}.install
depends=('python2' 'python2-cherrypy' 'python2-gobject'
         'python2-pybonjour' 'libnotify')
makedepends=('git')
source=('master'::'git://github.com/hauckwill/linconnect-server.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/master"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  install -dm755 "${pkgdir}/usr/"{bin,share/}
  cp -r "${srcdir}/master/LinConnectServer/main/" \
        "${pkgdir}/usr/share/linconnect-server"
  cat > "${pkgdir}/usr/bin/linconnect-server" <<EOF
#!/bin/sh
exec python2 /usr/share/linconnect-server/linconnect_server.py
EOF
  chmod +x "${pkgdir}/usr/bin/linconnect-server"
}
