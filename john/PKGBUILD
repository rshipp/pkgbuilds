# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=john
_jumbover=7
pkgver=1.7.9_jumbo${_jumbover}
_pkgver=1.7.9
pkgrel=1
pkgdesc="John the Ripper - A fast password cracker (jumbo-$_jumbover included)"
arch=('i686' 'x86_64')
url='http://www.openwall.com/john/'
license=('GPL2' 'custom')
depends=('openssl')
backup=('etc/john/john.conf')
source=("http://openwall.com/john/g/${pkgname}-${_pkgver}-jumbo-${_jumbover}.tar.gz"
        'params.h.patch')
md5sums=('fbca743b51a503ced1aa29d3933123c7'
         'f69ed632eba8fb9e45847a4b4a323787')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}-jumbo-${_jumbover}/src"

  patch -p0 < "${srcdir}/params.h.patch"

  if [ "$CARCH" == "x86_64" ]; then
    make linux-x86-64
  else 
    make linux-x86-mmx
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}-jumbo-${_jumbover}"

  # config file
  sed -i 's|$JOHN|/usr/share/john|g' "run/john.conf"
  install -Dm644 "run/john.conf" "${pkgdir}/etc/john/john.conf"

  # docs
  install -dm755 "${pkgdir}/usr/share/doc/john"
  install -m644 doc/* "${pkgdir}/usr/share/doc/john/"
  install -dm755 "${pkgdir}/usr/share/john/"
  install -Dm644 "doc/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # install password list and charset files
  install -m644 run/{{all,alnum,alpha,digits,lanman}.chr,password.lst} \
    "${pkgdir}/usr/share/john/"
  install -m644 run/{dumb16,dumb32,dynamic}.conf \
    "${pkgdir}/usr/share/john/"

  # install binaries
  install -Dm755 "run/john" "${pkgdir}/usr/bin/john"
  install -Dm755 "run/mailer" "${pkgdir}/usr/bin/john-mailer"
  cd "${pkgdir}/usr/bin"
  for s in unafs unique unshadow undrop pdf2john rar2john ssh2john zip2john; do
    ln -s john $s
  done
}
