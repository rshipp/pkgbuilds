# Maintainer: george <rpubaddr0 {at} gmail [dot] com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Alex Anthony <alex.anthony28991@gmail.com>

pkgname=python2-markupsafe
_pkgname=MarkupSafe
pkgver=0.15
pkgrel=1
pkgdesc='Implements a XML/HTML/XHTML Markup safe string for Python.'
arch=('i686' 'x86_64')
url='http://pypi.python.org/pypi/MarkupSafe'
license=('BSD')
makedepends=('python-distribute')
depends=('python2')
source=("http://pypi.python.org/packages/source/M/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('4e7c4d965fe5e033fa2d7bb7746bb186')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python2-markupsafe/LICENSE"
}
