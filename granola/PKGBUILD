# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=granola
pkgver=5.0.11
pkgrel=1
pkgdesc="MiserWare intelligent power management for PCs"
arch=(i686 x86_64)
url=http://grano.la/
license=(custom)
optdepends=('python2: MiserWare customer support via the mw-feedback script'
    'granola-connect: daemon to upload energy stats'
    'granola-gui: Granola graphical interface')
backup=(etc/$pkgname.conf)
options=(!strip)
install=$pkgname.install
if [[ $CARCH = "i686" ]]; then
    _arch=i386
    sha256sums=('4a4b2ccea5ce232d59e0a3f756d51f6e8777eb5ae42993fb05d7d98ec76d9846')
    sha512sums=('846f5cfe70cd32a50cb3cb24b30a4109ba32dae8e1f5f0bbf9bc88f7273120b2fe1f4867b3800f0373beac654c7e3e15c00ee4e234d11862e39053ad9efec250')
else
    _arch=$CARCH
    sha256sums=('8144a196f9de3e4f996d9ec4b0d4f9ac1a09d1de2464151c454da80b4fd7fbf0')
    sha512sums=('09908ac4bf06b231527c9a7ae991e7bbc13006ee45ffda12756fd0761ab1c4b97017e6ece2c7d4d14372123c8a0328e1462e92068981376239742eba4c90632a')
fi
source=(https://download.miserware.com/linux/tar/$_arch/$pkgname-$pkgver-$_arch.tar.gz
    $pkgname.confd
    $pkgname.rcd.sh
    $pkgname.service)
sha256sums+=('b2f5e32c820dc692d37568883a4e8ef254b0feafa8da0952b4904596bbc192c4'
    'f57c31baa80d3d0ad0618190c05acb65f0736ba6dc456ec3ce49a5b5b1aea43e'
    '519da90556348052f82625fff37951633e632c3794e6adbeb2294576673dbb31')
sha512sums+=('c9a5d92a9af069ad6b4c2a65ad7d8c840d1679db7e9f87cd65c5b23f57de2001dd27856c8f4bd0b14b34cc5c99b3e5b4bcefb4029f235714873e9ddf205c6ed2'
    '52c432797f790e71c16794a55e572d5d9282b2bf3eaea0495667a5e4fdae0c1309e7af8e1408bb7c6114b280fe765f9267fd87dca9e7450b25ff71996a6da2c8'
    '1f2fab3d6bbce2180de0680c8ce1a87965456ae1808707b9deb4af59a819616a83dfefac7d8156207efdc570546634bc08d72710db6c37bd828d5122894cdafb')

prepare() {
    cd $pkgname-$pkgver-$_arch/
    sed -i 's:^#!/usr/bin/env python$:&2:' usr/bin/mw-feedback
    mv usr/{sbin,bin}/$pkgname
    rmdir usr/sbin/
}

package() {
    cd $pkgname-$pkgver-$_arch/

    cp -r usr var "$pkgdir"
    chmod 755 "$pkgdir"/var/lib/miserware
    chmod 644 "$pkgdir"/var/lib/miserware/*
    install -Dm644 etc/$pkgname.conf "$pkgdir"/etc/$pkgname.conf

    install -Dm644 ../$pkgname.confd "$pkgdir"/etc/conf.d/$pkgname.conf
    install -Dm755 ../$pkgname.rcd.sh "$pkgdir"/etc/rc.d/$pkgname
    install -Dm644 ../$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service

    rm -rf "$pkgdir"/usr/share/doc/
    install -Dm644 usr/share/doc/$pkgname-$pkgver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
